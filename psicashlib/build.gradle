apply plugin: 'com.android.library'

android {
    compileSdkVersion 23

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        externalNativeBuild {
            cmake {
                cppFlags "-frtti -fexceptions"
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            externalNativeBuild {
                cmake {
                    version "3.10.2"

                    // To keep the size small, do not include all ABIs in release build
                    abiFilters "armeabi-v7a", "arm64-v8a"
                }
            }
        }

        debug {
            testCoverageEnabled true
        }
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

task recordGitVersion() {
    description = "Output git state file"

    doLast {
        def gitVer = new ByteArrayOutputStream()
        exec {
            commandLine "git", "describe", "--always", "--long", "--dirty", "--tags"
            standardOutput = gitVer
        }
        new File("$projectDir/src/main/assets/git.txt").text = gitVer.toString().trim()
    }
}

afterEvaluate { project ->
    assembleRelease.dependsOn tasks.getByName('recordGitVersion')
    assembleDebug.dependsOn tasks.getByName('recordGitVersion')
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:23.4.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support:support-annotations:23.4.0'
    androidTestImplementation 'com.android.support.test:runner:0.5'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:2.2.1'
}
